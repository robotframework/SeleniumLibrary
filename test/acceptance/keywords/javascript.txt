*** Setting ***
Resource  ../resource.txt
Test Setup  Go To  ${ROOT}javascript/dynamic_content.html
Default Tags  javascript


*** Test Cases ***
Clicking Elements Should Activate Javascript
  Title Should Be  Original
  Click Element  link=change title  And Don't Wait
  Title Should Be  Changed

Alert Should Be Present
  [Documentation]  FAIL Alert text should have been 'foo bar' but was 'ALERT!'
  [Setup]  Go To  ${ROOT}javascript/alert.html
  Click Link  Click Me!  And Don't Wait
  Alert Should Be Present
  Click Link  Click Me Too!  And Don't Wait
  Alert Should Be Present  MULTILINE ALERT!
  Click Link  Click Me!  And Don't Wait
  Alert Should Be Present  foo bar

Get Alert Message
  [Documentation]  FAIL There were no alerts
  [Setup]  Go To  ${ROOT}javascript/alert.html
  Click Link  Click Me!  And Don't Wait
  ${msg} =  Get Alert Message
  Should Be Equal  ${msg}  ALERT!
  ${msg} =  Get Alert Message

Mouse Down On Link
  [Setup]  Go To  ${ROOT}javascript/mouse_events.html
  Mouse Down On Image  image_mousedown
  Text Field Should Contain  textfield  onmousedown
  Input text  textfield  \
  Mouse Down On Link  onmousedown
  Text Field Should Contain  textfield  onmousedown

Wait For Condition
  [Documentation]  FAIL Condition 'window.document.title == "Invalid"' did not become true in 0.1
  ...  LOG 2 Waiting 5 seconds for condition 'window.document.title == "Changed"' to be true.
  ...  LOG 3 Waiting 0.1 for condition 'window.document.title == "Invalid"' to be true.
  [Setup]  Go To  ${ROOT}javascript/delayed_events.html
  Title Should Be  Original
  Wait For Condition  window.document.title == "Changed"
  Wait For Condition  window.document.title == "Invalid"  ${0.1}

Wait Until Page Contains
  [Documentation]  FAIL Text 'invalid' did not appear in '0.1'
  [Setup]  Go To  ${ROOT}/javascript/delayed_events.html
  Wait Until Page Contains  New Content  2 s
  Wait Until Page Contains  invalid  0.1

Wait Until Page Contains Element
  [Documentation]  FAIL Element 'non-existent' did not appear in '0.1 seconds'
  [Setup]  Go To  ${ROOT}/javascript/delayed_events.html
  Wait Until Page Contains Element  new div  2 seconds
  Wait Until Page Contains Element  non-existent  0.1 seconds

Confirm Action
  Click Button  Change the title  and don't wait
  ${msg}=  Confirm Action
  Title Should Be  Changed after confirmation
  Should Be Equal  ${msg}  Really change the title?

Cancel Action
  Choose Cancel On Next Confirmation
  Click Button  Change the title  and don't wait
  ${msg}=  Confirm Action
  Title Should Be  Original
  Should Be Equal  ${msg}  Really change the title?

Execute Javascript
  [Documentation]  LOG 1 Executing JavaScript:\n
  ...  window.add_content('button_target', 'Inserted directly')
  Execute Javascript  window.add_content('button_target', 'Inserted directly')
  Page Should Contain  Inserted directly

Execute Javascript from File
  [Documentation]  LOG 1:1 REGEXP: Reading JavaScript from file .*
  ...  LOG 1:2 Executing JavaScript:\n
  ...  window.add_content('button_target', 'Inserted via file')
  Execute Javascript  ${CURDIR}/executed_by_execute_javascript.js
  Page Should Contain  Inserted via file

Open Context Menu
  Go To  ${ROOT}/javascript/context_menu.html
  Open Context Menu  myDiv
