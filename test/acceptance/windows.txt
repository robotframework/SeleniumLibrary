*Setting*
Documentation  These tests must open own browser because windows opened by
...  earlier tests would otherwise be visible to Get Window XXX keywords
...  even if those windows were closed.
Suite Setup  Open Browser To Start Page Without Testing Default Options
Test Setup  Go To Page "javascript/popupwindow.html"
Suite Teardown  Close All Browsers
Resource  resource.txt

*Test Cases*
Popup Windows Created With Javascript
  Cannot Be Executed in IE
  Open Popup Window, Select It And Verify  myName
  Do Action In Popup Window And Verify
  Select Main Window And Verify

Get Window Identifiers
  Click Link  my popup
  @{window_ids}=  Get Window Identifiers
  Length Should Be  ${window_ids}  2
  Select Window  @{window_ids}[0]
  Title Should Be  Click link to show a popup window
  Select Window  @{window_ids}[1]
  Title Should Be  Original

*Keywords*
Open Popup Window, Select It And Verify
  [Arguments]  ${window_id}
  Click Link  my popup
  Select Window  ${window_id}
  Title should Be  Original

Select Main Window And Verify
  Close Window
  Select Window  main
  Title Should Be  Click link to show a popup window

Do Action In Popup Window And Verify
  Click Link  change title
  Title Should Be  Changed
